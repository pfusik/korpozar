DEBUG	equ	0

font1	equ	$2000
font2	equ	$2400
scr	equ	$3740
scr_aligned	equ	scr&$ff00
scr_end	equ	$3b00

shade	equ	$80
tempoctr	equ	shade

	ift	COMPATIBLE
threshold	equ	$83
	org	$3600
	els
	org	$80
	eif
main

	ift	DEBUG
	mwa	#vbl	$222
	eif

	ldy	#0
	ift	COMPATIBLE
ptr	equ	$81
	sty	ptr
	lda	#$19
	els
ptr	equ	*-1
	eif

	ldx	>font1
	stx	$2c8
	jsr	init_font
	lda	#$09
:!COMPATIBLE	sta	threshold
	ldx	>font2
	jsr	init_font

	ift	COMPATIBLE
	mva	$231	ptr+1
	ldy	#$25
	mva	>scr	(ptr),y
	els
	mva	>scr	$bc25
	eif
	mva	#audc	^21
startmsx
:COMPATIBLE	mva	<notes	noteptr
:!COMPATIBLE	mva	<notes	z:noteptr

playnote
	lda	notes
noteptr	equ	*-2
	bmi	rest
	beq	startmsx
	sta	^20
	lda	#$80+tempo/2
rest
	sta	tempoctr
	inc	noteptr

loop
	lda	#17-3
	cmp:rne	^4b

	dec	tempoctr
	bpl	playnote

	ldy	#40
light_my_fire
	lda	^2a
	and	#$3f
	sta	scr_end,y
	dey
	bpl	light_my_fire

;	lsr	^1a
	ldy	<scr
	sty	$26f
	ldx	>scr
fire_page
	ift	0
	lda	20
	and	#1
	add	>font1+$300
	eli	COMPATIBLE
	lda	font_interlace->scr,x
	els
	lda	$ee52->scr,x
	eor	#$0b^>font1
	eif
	sta	$2f4
;	lda	^4b
;	sta	$600
;	inc	*-2
	ift	COMPATIBLE
	stx	fire_pixel+2
	stx	fire_pixel+5
	stx	fire_pixel+8
	stx	fire_pixel+11
	stx	fire_store+2
	els
	stx	z:fire_pixel+2
	stx	z:fire_pixel+5
	stx	z:fire_pixel+8
	stx	z:fire_pixel+11
	eif
fire_pixel
	lda	scr_aligned,y
	adc	scr_aligned+39,y
	adc	scr_aligned+40,y
	adc	scr_aligned+41,y
	sbc	#2
	bcc	fire_low
	lsr	@
	lsr	@
	ift	COMPATIBLE
fire_store
	sta	scr_aligned,y
	els
	sta	(fire_pixel+1),y
	eif
fire_low
	iny
	bne	fire_pixel
	inx
	cpx	>scr_end
	bcc	fire_page
;	dec	^1a
	bcs	loop

init_font
:COMPATIBLE	sta	threshold
	sty	shade
	jsr	init_font_page
	inx
init_font_page
	stx	ptr+1
init_font_line
	tya
	and	#$19
	ift	COMPATIBLE
	cmp	threshold
	els
	cmp	#$19
threshold	equ	*-1
	eif
	lda	#0
	scc:lda	#$10
	adc	shade
	sta	(ptr),y
	iny
	tya
	and	#$1f
	bne	init_font_line
	lda	#$11
	adc:sta	shade
	tya
	bne	init_font_line
	rts

	ift	DEBUG
vbl
	mva	$2f4	$600
	inc	*-2
	jmp	$c0e2
	eif

	ift	COMPATIBLE
font_interlace	dta h(font2,font2,font1,font1)
	eif

	icl	'msx.asx'

	ift	COMPATIBLE
	run	main
	eif
